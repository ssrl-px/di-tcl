#!/usr/bin/tclsh
#
package require Itcl
namespace import ::itcl::*
package require DependencyInjector 3.2
package require tcltest


class Person {
    inherit DI::Bean 
    public variable person_id
    public variable slac_id
    public variable safetyTalk
    public variable first_name
    public variable last_name
    public variable middle_name
    public variable email_address
    public variable citizenship
    public variable safetyAgreementDate

    public variable address
    public variable userAgreement

    public method afterPropertiesSet {} {
        assertPropertyIsaClass address Address
        assertPropertyNotEmpty last_name
    }
}

class Address {
    inherit DI::Bean
    public variable address1
    public variable address2
    public variable city
    public variable state
    public variable zipcode
    public variable country
    public variable institution
    public method afterPropertiesSet {} {
    }
}

class Institution {
    inherit DI::Bean 
    public variable name
    public variable insti_id
    public method afterPropertiesSet {} {}
}


class Team {
    inherit DI::Bean 
    public variable people
    public method afterPropertiesSet {} {
        assertEachListMemberIsaClass people Person
    }
}

class Car {
    inherit DI::Bean
    public variable make
    public variable year

    public method afterPropertiesSet {} {
        assertPropertyNotEmpty make
        assertPropertyNotEmpty year
    }
}

variable SETUP {
    set filename ../resources/People.properties
    set __di [DI::DependencyInjector #auto]
    $__di load $filename
}

variable CLEANUP {
    delete object $__di
    delete object $obj1
}

    #beamline is the first argument when starting the program
tcltest::test createObjectFromDependencyInjection {normal return} \
    -setup $SETUP -cleanup $CLEANUP \
    -body {
    set obj1 [$__di createObjectByName joe]
    return [$obj1 cget -last_name]
} -result johnson


tcltest::test createObjectFromDependencyInjection2 {normal return} \
    -setup $SETUP -cleanup $CLEANUP \
    -body {
    set obj1 [$__di createObjectByName joe]
    set address [$obj1 cget -address]
    return [$address cget -city]
} -result "Los Angeles"


tcltest::test createObjectFromDependencyInjectionWithEquals {normal return} \
    -setup $SETUP -cleanup $CLEANUP \
    -body {
    set obj1 [$__di createObjectByName joe]
    set address [$obj1 cget -address]
    return [$address cget -address1]
} -result "University of California = Los Angeles"


tcltest::test createObjectFromDependencyInjection3 {normal return} \
    -setup $SETUP -cleanup $CLEANUP \
    -body {
        set obj1 [$__di createObjectByName team]
        set people [$obj1 cget -people]

        set names [list]
        foreach person $people {
            lappend names [$person cget -last_name]
        }
        return $names
    } -result [list johnson Peterson johnson]

tcltest::test testAssertFromDependency {failed assert test} \
    -setup $SETUP \
    -body {
        $__di createObjectByName badteam
    } -returnCodes 1 -result "::DI::DependencyInjector::address8 in 'badteam.people' must be instantiated from one of : Person"

tcltest::test trimFirst {normal return} {
    DI::StringUtil::trimFirst HelloThere Hello
} There

tcltest::cleanupTests
